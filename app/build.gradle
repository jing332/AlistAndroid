plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("kotlin-parcelize")
    id("kotlinx-serialization")
    id("com.google.devtools.ksp")
    id("com.mikepenz.aboutlibraries.plugin")
}

Properties pro = new Properties()
InputStream input = rootProject.file("local.properties").newDataInputStream()
pro.load(input)
input.close()

static def releaseTime() {
    return new Date().format("yy.MMddHH", TimeZone.getTimeZone("GMT+8"))
}

def version = "1." + releaseTime()
def gitCommits = Integer.parseInt('git rev-list HEAD --count'.execute().text.trim())
android {
    namespace 'com.github.jing332.alistandroid'
    compileSdk 34
    defaultConfig {
        applicationId 'com.github.jing332.alistandroid'
        minSdkVersion 21
        targetSdk 34
        versionCode gitCommits
        versionName version

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.incremental", "true")
            arg("room.expandProjection", "true")
        }

        buildConfigField "String", "ALIST_VERSION", "\"" + pro["ALIST_VERSION"] + "\""
        buildConfigField "String", "ALIST_COMMIT_SHA", "\"" + pro["ALIST_COMMIT_SHA"] + "\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    signingConfigs {
        release {
            storeFile file(pro["KEY_PATH"])
            storePassword pro["KEY_PASSWORD"]
            keyAlias pro["ALIAS_NAME"]
            keyPassword pro["ALIAS_PASSWORD"]
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
//            manifestPlaceholders = [applicationId: "com.github.jing332.alistandroid"]

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.release

            minifyEnabled false
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationIdSuffix ".debug"
            versionNameSuffix "_debug"
//            manifestPlaceholders = [applicationId: "com.github.jing332.alistandroid.debug"]

            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            java.srcDirs = ['src/main/java', 'src/main/kotlin']
        }
    }

    splits {
        abi {
            enable gradle.startParameter.taskNames.any { it.contains("Release") }
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.7"
    }
    compileOptions {
//        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    kotlin {
        jvmToolchain(17)
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            //noinspection GrDeprecatedAPIUsage
            def abiName = output.getFilter(com.android.build.OutputFile.ABI)
            if (abiName == null)
                output.outputFileName = "AList-v${variant.versionName}.apk"
            else
                output.outputFileName = "AList-v${variant.versionName}_${abiName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation project(":utils")

    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation("com.iqiyi.xcrash:xcrash-android-lib:3.0.0")

    implementation("com.mikepenz:aboutlibraries-core:${about_lib_version}")
    implementation("com.mikepenz:aboutlibraries-compose:${about_lib_version}")
    implementation("org.burnoutcrew.composereorderable:reorderable:0.9.6")

    implementation("com.github.jeziellago:compose-markdown:0.3.4")

    implementation("androidx.documentfile:documentfile:1.0.1")
    implementation("androidx.core:core-ktx:1.12.0")

    implementation("io.coil-kt:coil-compose:2.4.0")
    implementation("com.charleskorn.kaml:kaml:0.55.0")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0")

    // Room
    implementation("androidx.room:room-runtime:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    ksp("androidx.room:room-compiler:$room_version")
    androidTestImplementation("androidx.room:room-testing:$room_version")

    // IO & NET
    implementation 'com.squareup.okio:okio:3.3.0'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.github.liangjingkanji:Net:3.6.4'

    implementation("com.google.android.exoplayer:exoplayer-core:2.19.1")

    implementation("org.apache.commons:commons-lang3:3.12.0")
    implementation("com.github.FunnySaltyFish.ComposeDataSaver:data-saver:v1.1.8")
    implementation("com.louiscad.splitties:splitties-systemservices:3.0.0")
    implementation("androidx.localbroadcastmanager:localbroadcastmanager:1.1.0")

    def accompanistVersion = "0.31.3-beta"
    implementation("com.google.accompanist:accompanist-systemuicontroller:${accompanistVersion}")
    implementation("com.google.accompanist:accompanist-navigation-animation:${accompanistVersion}")
    implementation("com.google.accompanist:accompanist-permissions:${accompanistVersion}")
    //webview
    implementation("androidx.webkit:webkit:1.9.0")
    implementation("com.google.accompanist:accompanist-webview:${accompanistVersion}")


    implementation("androidx.constraintlayout:constraintlayout-compose:1.0.1")
    implementation("androidx.navigation:navigation-compose:2.7.6")

    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2'
//    def composeBom = platform('androidx.compose:compose-bom:2023.06.01')
    def composeBom = platform("dev.chrisbanes.compose:compose-bom:2023.12.00-alpha04")
    implementation composeBom
    androidTestImplementation composeBom

    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.foundation:foundation")
    implementation("androidx.compose.ui:ui")

    implementation 'androidx.compose.material:material-icons-core'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.material3:material3-window-size-class'

    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'


}
